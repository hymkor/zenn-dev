---
title: "S式で Makefile を書く make を作ったよ！"
emoji: "📘"
type: "tech" # tech: 技術記事 / idea: アイデア
topics: ["Lisp","Makefile"]
published: false
---
### Go言語で ISLisp のサブセットを作ってる

- [hymkor/gmnlisp: gmnlisp](https://github.com/hymkor/gmnlisp)

でも、単に規格書に載ってる標準関数を一つずつ実装してゆくたけというのもつまらんなぁ。
何か実用的なもんを作ってみたいなぁ
もともと、自分が作る GO アプリのカスタマイズ言語として作ったもんだしなぁ。

そういえば、前にどこの環境(Windows、Linux) でも動くビルドスクリプトを書こうとしたが、
結局、GNU Make 依存で、nmake 環境でも大丈夫とまではいかなかった

なら、S式でビルドルールを書くMakeを作ればいいんじゃね？

### ということで作ってみた。

[hymkor/smake: SMake (Make by S-expression)](https://github.com/hymkor/smake)

- Makefile.lsp に make のルールを記述する. 
- Makefile.lsp 自体は純粋な Lisp スクリプト。これを解釈する smake には (make) というビルドを行う関数が追加されている.
- (make) 関数の仕様は、Lisp の (cond) や (case) に似た形で引数を与える.
    - 第一パラメータは、ビルドする最終ターゲット名
        - サンプルでは $1 (smakeコマンド自身の第一引数が入っている)
    - 第二パラメータ以降は、ルール定義のリスト
        - リストの第一要素は、（ターゲット ソース…) を表すリスト。評価されるので、動的にルールを作ることが可能.
        - リストの第二要素以降は、ソースがターゲットよりも新しい時に実行される Lisp コマンド郡

```Makefile.lsp
(let*
  ((windows (equal (getenv "OS") "Windows_NT"))
   (EXE (if windows ".exe" "")))
  (make
    $1
    ((cons "smake$(EXE)" (glob "*.go"))
     (sh "go fmt")
     (sh "go build")
     )
    ('("get")
     (sh "go get -u")
     (sh "go mod tidy")
     )
    ('("update")
     (touch "main.go")
     )
    ('("readme" "README.md")
     )
    ('("README.md" "_README.md" "Makefile.lsp")
       (sh "gmnlpp$(EXE) $< > \"$@\"")
     )
    ('("clean")
     (rm "smake$(EXE)~")
     )
  )
)
```

make 相当の機能を簡単に実現できるよう組み込み関数も追加した。

- sh - シェルコマンドを実行する。sh や CMD.EXE で実行される
- touch - ファイルのタイムスタンプを更新する。Windows には存在しない場合があるから
- rm - ファイルを削除する。UNIX系だと rm , Windows だと del と、コマンド名がばらつくから
- glob - ワイルドカード展開をするぞ。戻り値はリストだ。
- getenv - 環境変数を取得するぞ
- \*args\* - smake コマンドに与えたパラメータが入ったリストだ。また、リストか
- $1,$2,$3... - \*args\* そのままだと使いにくいから、ばらしたやつ

あと、文字列の中にデータを埋め込むのは結構めんどうなので、書く文字列を受け取る関数は文字列の中の `$(...)` といった語句を解釈して、変数の値を展開する機能をつけてあるよ（文字列自体の機能ではないので、評価のタイミングが遅いのに注意）

### ルールを動的に作ることも可能だ！

ルールが S 式で表現されるということは、オンメモリでルールを生成することもできるということを意味している。

C言語向け Makefile の

```Makefile
a.out: $(OBJS)
    gcc -o $@ $(OBJS)
.c.o:
    gcc -c $<
```

に相当するものを書いてみよう.

``` Makefile.lsp
(labels
  ;;; 下請け関数群の定義 ;;;
  ((c-to-o (c)
           (string-append (subseq c 0 (- (length c) 2)) ".o"))
   (string-join
     (dem seq)
     (if seq
       (apply #'string-append (cdr (mapcan (lambda (c) (list dem c)) seq)))
       ""))
    (getfname (path)
      (let ((index nil))
        (while (setq index (string-index $/ path))
               (setq path (subseq path (1+ index)))
        )
        path)
    )
   ) ; flet param
  (let*
    ((c-files (glob "*.c"))
     (o-files (mapcar #'c-to-o c-files))
     (windows (equal (getenv "OS") "Windows_NT"))
     (exe (if windows ".exe" ""))
     (cwd (qs (if windows "cd" "pwd")))
     (a-out (string-append (getfname cwd) exe))
     )
    (apply
      #'make
      $1

      ; 固定ルール
      ;     実行ファイル名: オブエジェクトファイル群...
      ;          gcc -o $@ オブエジェクトファイル群...
      ; に相当する
      ((cons a-out o-files)
       (sh (string-join " " (cons "gcc -o $@" o-files)))
       )

      ; 固定ルール
      ;     clean:
      ;         rm 実行ファイル オブジェクトファイル
      ; に相当
      ('("clean")
       (apply #'rm a-out o-files)
       )

      ; 可変ルールC：ファイルごとに
      ;     オブジェクトファイル : Cファイル
      ;          gcc -c $<
      ; を mapcar で作ってゆく.
      (mapcar
        (lambda (c-fname)
          (list
            (list 'quote (list (c-to-o c-fname) c-fname))
            '(sh "gcc -c $<")
            )
          )
        c-files)
      )
  ) ; let* code
) ; flet code
```

apply は後続するリストを引数にして関数を実行する関数です
( リストと関数名の間のパラメータは、そのまま関数に与えるという点がちょっとトリッキーですが )。
それを利用して `.c.o: gcc -c $<>` をスクリプトだけで実現しています。